import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm' version '1.8.0'
    id 'com.diffplug.spotless' version '6.16.0'
    id 'org.sonarqube' version '4.0.0.2929'
    id("org.jetbrains.kotlinx.kover") version "0.6.1"
    id 'com.worksap.nlp.sudachi.es'
}

group = 'com.worksap.nlp'
archivesBaseName = 'analysis-sudachi'
version = '3.0.2-SNAPSHOT'

tasks.withType(JavaCompile).configureEach {
    options.release.set(11)
    options.encoding = 'UTF-8'
}

compileKotlin {
    compilerOptions.jvmTarget.set(JvmTarget.JVM_11)
}

compileTestKotlin {
    compilerOptions.jvmTarget.set(JvmTarget.JVM_11)
}

repositories {
    mavenLocal()
    mavenCentral()
    if (project.version.endsWith("-SNAPSHOT")) {
        maven {
            url = uri("https://oss.sonatype.org/content/repositories/snapshots/")
        }
    }
}

dependencies {
    implementation('com.worksap.nlp:sudachi:0.7.1')
    testImplementation('org.apache.logging.log4j:log4j-core:2.17.2')
    testImplementation('org.jetbrains.kotlin:kotlin-test-junit') {
        exclude(group: 'org.hamcrest')
    }
}

def embedVersion = tasks.register('embedVersion', Copy) {
    from 'src/main/extras/plugin-descriptor.properties'
    into 'build/descriptor'
    var esKind = sudachiEs.kind.get()
    expand([version: version, elasticsearchVersion: esKind.version])
    inputs.property("version", version)
    inputs.property("elasticSearchVersion", esKind.version)
}

def copyDependencies = tasks.register('copyDependencies', Copy) {
    from configurations.runtimeClasspath
    into 'build/libs'
}

def distZip = tasks.register('distZip', Zip) {
    var esKind = sudachiEs.kind.get()
    dependsOn tasks.jar, embedVersion, copyDependencies
    archiveBaseName.set(archivesBaseName)
    archiveAppendix.set(esKind.version)
    from 'build/libs/', 'build/descriptor', 'LICENSE', 'README.md'
}

artifacts {
    archives distZip
}

// See https://github.com/diffplug/spotless/tree/main/plugin-gradle
spotless {
    // watch for https://github.com/diffplug/spotless/issues/911 to be closed
    ratchetFrom 'origin/develop'
    encoding 'UTF-8' // all formats will be interpreted as UTF-8

    format 'misc', {
        target '*.gradle', '*.md', '.gitignore', '*.txt', '*.csv'

        trimTrailingWhitespace()
        indentWithSpaces(2)
        endWithNewline()
    }
    java {
        // don't need to set target, it is inferred from java
        // version list: https://github.com/diffplug/spotless/tree/main/lib-extra/src/main/resources/com/diffplug/spotless/extra/eclipse_jdt_formatter
        eclipse('4.21.0').configFile('.formatter/eclipse-formatter.xml')
        licenseHeaderFile('.formatter/license-header')
    }
    kotlin {
        // by default the target is every '.kt' and '.kts` file in the java sourcesets
        ktfmt('0.39')
        licenseHeaderFile('.formatter/license-header')
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "WorksApplications_elasticsearch-sudachi"
        property "sonar.organization", "worksapplications"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

tasks.koverXmlReport {
    enabled = true
    xmlReportFile.set(layout.buildDirectory.file("reports/jacoco/test/jacocoTestReport.xml"))
}

tasks.register("printVersionForGithubActions") {
    doLast {
        // https://docs.github.com/en/actions/using-workflows/workflow-commands-for-github-actions#environment-files
        file(System.getenv("GITHUB_ENV")).write("PROJ_VERSION=$project.version")
    }
}
