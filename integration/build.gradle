import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    id 'java-library'
    id 'com.worksap.nlp.sudachi.es'
    id 'com.worksap.nlp.sudachi.esc'
    id 'com.diffplug.spotless'
}

version = properties["pluginVersion"]

dependencies {
    compileOnly(project(':'))
    compileOnly(project(':spi'))
    testCompileOnly('junit:junit:4.13.1')
    testImplementation('org.apache.logging.log4j:log4j-core:2.17.2')
}

test {
    dependsOn(rootProject.tasks["packageJars"], rootProject.tasks["packageSpiJars"], rootProject.tasks["embedVersion"])
    def esKind = sudachiEs.kind.get()
    def packageDir = rootDir.toPath().resolve("build/package/${version}/${esKind.engine.kind}-${esKind.version}")
    systemProperty("sudachi.es.bundle", packageDir.toString())
    systemProperty("tests.gradle", "true")
    systemProperty("tests.security.manager", "false")
}

spotless {
    // watch for https://github.com/diffplug/spotless/issues/911 to be closed
    ratchetFrom 'origin/develop'
    encoding 'UTF-8' // all formats will be interpreted as UTF-8
    def formatter = rootProject.projectDir.toPath().resolve(".formatter")

    format 'misc', {
        target '*.gradle', '*.md', '.gitignore', '*.txt', '*.csv'

        trimTrailingWhitespace()
        indentWithSpaces(2)
        endWithNewline()
    }
    java {
        // don't need to set target, it is inferred from java
        // version list: https://github.com/diffplug/spotless/tree/main/lib-extra/src/main/resources/com/diffplug/spotless/extra/eclipse_jdt_formatter

        eclipse('4.21.0').configFile(formatter.resolve('eclipse-formatter.xml'))
        licenseHeaderFile(formatter.resolve('license-header'))
    }
}
